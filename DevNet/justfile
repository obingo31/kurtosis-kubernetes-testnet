install-ns-authz:
    kubectl apply -f ns-authz.yaml

uninstall-ns-authz:
    kubectl delete -f ns-authz.yaml

get-service-url enclaveName serviceName portId:
    kurtosis service inspect {{enclaveName}} {{serviceName}} | tail -n +2 | yq e - -o=json |\
        jq -r --arg portId {{portId}} '.Ports[$portId]' | sed 's/.*-> //'

open-service enclaveName serviceName:
    open "$(just get-service-url {{enclaveName}} {{serviceName}} http)"

open-grafana enclaveName:
    just open-service {{enclaveName}} grafana

fix-style:
    kurtosis lint . --format

lint-style:
    kurtosis lint .

# TODO(enable more checks)
lint-code:
    kurtosis-lint \
        --checked-calls \
        --local-imports \
        main.star src/ test/

lint: lint-style lint-code

test:
    @echo "No tests configured yet. Add test files to test/ directory and implement test runner."

configure-grafana name url token:
    grr config create-context {{name}}
    grr config set grafana.url {{url}}
    grr config set grafana.token {{token}}
    grr config set output-format json

use-context name:
    grr config use-context {{name}}

push name:  # âœ… 'name' is a parameter
    grr config use-context {{name}}
    # We must explicitly provision folders prior to dashboards due to a bug in `grizzly`.
    # Note the `-e` flag (`--continue-on-error`), which is required as many of the exported dashboards currently contain syntax errors due to Grafana quirks.
    # The `---disable-reporting` is required on every command as `grizzly` does not support enviornment variable configuration fully.
    grr push resources/folders -e --disable-reporting
    grr push resources/dashboards -e --disable-reporting

pull name:
    grr config use-context {{name}}
    grr pull resources/folders --disable-reporting
    grr pull resources/dashboards --disable-reporting

list-resources name:
    grr config use-context {{name}}
    grr list resources/folders --disable-reporting
    grr list resources/dashboards --disable-reporting